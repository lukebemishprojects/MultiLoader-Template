plugins {
    id 'shared'
    alias libs.plugins.sponge.vanillagradle
}

minecraft {
    version(libs.versions.minecraft.get())
    if(file("src/main/resources/${mod_id}.accesswidener").exists()){
        accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
    }
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
    commonAccessWidener {
        canBeResolved = false
        canBeConsumed = true
    }
}

dependencies {
    compileOnly libs.sponge.mixin
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
    var accessWidener = project.file("src/main/resources/${mod_id}.accesswidener")
    if (accessWidener.exists()) {
        commonAccessWidener accessWidener
    }
}
